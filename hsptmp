;#regcmd "_hsp3cmdinit@4", "ランタイム\\ovplay.dll"
#define debug 0;デバック用、終わったら0に戻す
#define allinclude 0;実行ファイル内に全ての画像や音声を詰め込む場合は1にする（未実装）
#define FIRSTSTAGE 1;最初のステージ、基本1

	#define GAMENAME "SUPER3"
	#define DEVELOPER "製作者:ときえのきと愉快な仲間たち\nhttps://github.com/jikantoki\n\nBGMと効果音は魔王魂またはポケットサウンドのものを使用\nhttps://maoudamashii.jokersounds.com/\nhttps://pocket-se.info/"
	#packopt name "game.exe"

/* 画像についての注意点
背景を黒(R,G,B=0,0,0)にしてください、右の命令のモード2を使用します(F1で解説を開く) ⇒ gmode
逆に画像に黒(R,G,B=0,0,0)を使用しないでください、黒を使いたい場合(R,G,B=1,1,1)をご使用ください。 */

#include "hspogg.as"
dmmini;重要
#include "ovplay.as"
exist "ovplay.dll"
if strsize=-1:end
font msgothic,40
mes "ちょっと待ってね"
#include "fps.hsp"
#define STAGE "ステージ\\"
#define GAZOU "画像フォルダ\\"
#define MAGNIFICATION 3

#define WINDOWX 1280
#define WINDOWY 780

#define MAXSPEED 5.0

;タイトル
#define TITLEM_NAME "はじめから,つづきから,操作説明,製作者情報,おわる"
#define TITLEM_SIZE 50
#define TITLEM_BUTTON_SIZE 10
#define TITLEM_SECURE 20
#define TITLEM_POS_Y 410
#define STSEL_POS_X 250
#define STSEL_POS_Y 430
#define STSEL_SECURE 10

#const BUTTONSIZEX TITLEM_SIZE*5 + TITLEM_BUTTON_SIZE
#const BUTTONSIZEY TITLEM_SIZE + TITLEM_BUTTON_SIZE
#const CHARASIZE 14*MAGNIFICATION
#const CHARASHIFT 1*MAGNIFICATION
#const BMAG 16*MAGNIFICATION

#enum AIR = 0
#enum ERROR = 200
#enum BASIC
celload GAZOU+"error.png",BASIC : buffer ERROR,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC 
#enum BUTTON1SCR
#enum BUTTON3SCR
#enum BUTTON5SCR
/*
#if allinclude=1
	#pack "画像フォルダ"
	#pack "音声フォルダ"
	#pack "ステージ"
#endif
*/
#epack "851Gkktt.ttf"

#enum BUTTONA1
celload "画像フォルダ\\ボタンA1.png",BUTTONA1
#enum BUTTONB1
celload "画像フォルダ\\ボタンB1.png",BUTTONB1
#enum BUTTONC1
celload "画像フォルダ\\ボタンC1.png",BUTTONC1
#enum BUTTONA3
celload "画像フォルダ\\ボタンA3.png",BUTTONA3
#enum BUTTONB3
celload "画像フォルダ\\ボタンB3.png",BUTTONB3
#enum BUTTONC3
celload "画像フォルダ\\ボタンC3.png",BUTTONC3
#enum BUTTONA5
celload "画像フォルダ\\ボタンA5.png",BUTTONA5
#enum BUTTONB5
celload "画像フォルダ\\ボタンB5.png",BUTTONB5
#enum BUTTONC5
celload "画像フォルダ\\ボタンC5.png",BUTTONC5

#enum ACWAV
mmload "音声フォルダ\\allclear.wav",ACWAV
#enum BBWAV
mmload "音声フォルダ\\blockbreak.wav",BBWAV
#enum CLWAV
mmload "音声フォルダ\\clear.wav",CLWAV
#enum COWAV
mmload "音声フォルダ\\coin.wav",COWAV
#enum DTWAV
mmload "音声フォルダ\\death.wav",DTWAV
#enum JPWAV
mmload "音声フォルダ\\jump.wav",JPWAV
#enum KLWAV
mmload "音声フォルダ\\kill.wav",KLWAV
#enum OKWAV
mmload "音声フォルダ\\ok.wav",OKWAV
#enum EDWAV
mmload "音声フォルダ\\ending.wav",EDWAV
#enum GOWAV
mmload "音声フォルダ\\gameover.wav",GOWAV
#enum PUWAV
mmload "音声フォルダ\\powerup.wav",PUWAV
#enum TYWAV
mmload "音声フォルダ\\tyukan.wav",TYWAV
#enum CCWAV
mmload "音声フォルダ\\cancel.wav",CCWAV

#enum BGM1
dmmload "音声フォルダ\\bgm1.ogg",BGM1
dmmvol BGM1,-800
#enum BGM2
dmmload "音声フォルダ\\bgm2.ogg",BGM2
dmmvol BGM2,-800
#enum BGM3
dmmload "音声フォルダ\\bgm3.ogg",BGM3
dmmvol BGM3,-800
#enum BGM4
dmmload "音声フォルダ\\bgm4.ogg",BGM4
dmmvol BGM4,-800
#enum BGM5
dmmload "音声フォルダ\\bgm5.ogg",BGM5
dmmvol BGM5,-800
#enum BGM99
dmmload "音声フォルダ\\opening.ogg",BGM99

buffer BUTTON1SCR
	pos 0,0		: gzoom BUTTONSIZEY , BUTTONSIZEY , BUTTONA1 ,,,16,16
	pos 0,100	: gzoom BUTTONSIZEY , BUTTONSIZEY , BUTTONB1 ,,,16,16
	pos 0,200	: gzoom BUTTONSIZEY , BUTTONSIZEY , BUTTONC1 ,,,16,16

buffer BUTTON3SCR
	pos 0,0		: gzoom BUTTONSIZEX*3/5 , BUTTONSIZEY , BUTTONA3 ,,,16*3,16
	pos 0,100	: gzoom BUTTONSIZEX*3/5 , BUTTONSIZEY , BUTTONB3 ,,,16*3,16
	pos 0,200	: gzoom BUTTONSIZEX*3/5 , BUTTONSIZEY , BUTTONC3 ,,,16*3,16

buffer BUTTON5SCR
	pos 0,0		: gzoom BUTTONSIZEX , BUTTONSIZEY , BUTTONA5 ,,,16*5,16
	pos 0,100	: gzoom BUTTONSIZEX , BUTTONSIZEY , BUTTONB5 ,,,16*5,16
	pos 0,200	: gzoom BUTTONSIZEX , BUTTONSIZEY , BUTTONC5 ,,,16*5,16

/* 変化ありブロック追加の説明 (この下に作ってください)
#enum プログラム内で使う名前(以下[enum名前]) (ファイルは事前に画像フォルダに入れておく)
celload GAZOU+"ファイルの名前",BASIC : buffer enum名前,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC */
#enum BRICK
celload GAZOU+"レンガ.png",BASIC : buffer BRICK,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum HATENA
celload GAZOU+"疑問符.png",BASIC : buffer HATENA,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum TOMEI
buffer TOMEI,BMAG,BMAG : cls 4
#enum MAGMA
celload GAZOU+"マグマ.png",BASIC : buffer MAGMA,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum MAGMAB
celload GAZOU+"マグマキューブ.png",BASIC : buffer MAGMAB,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum JUMPB
celload GAZOU+"ジャンプ.png",BASIC : buffer JUMPB,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum UNDERMAGMA
buffer UNDERMAGMA,BMAG,BMAG : color 237,28,36 : boxf
#enum COIN
#enum COINITEM
celload GAZOU+"コイン.png",BASIC : buffer COIN,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
buffer COINITEM,32,32 : gzoom 64,64,BASIC 
#enum GOALPAUL
celload GAZOU+"ゴールポール.png",BASIC : buffer GOALPAUL,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum GOALBALL
celload GAZOU+"ゴール上.png",BASIC : buffer GOALBALL,BMAG*2,BMAG*2 : gzoom BMAG*2,BMAG*2,BASIC
#enum NOITEM
celload GAZOU+"アイテムなしブロック.png",BASIC : buffer NOITEM,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum INTERP
celload GAZOU+"中間フラッグ.png",BASIC : buffer INTERP,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum INTERPMAE
celload GAZOU+"中間フラッグ取得前.png",BASIC : buffer INTERPMAE,BMAG,BMAG : gzoom BMAG*2,BMAG*2,BASIC
#enum CASTLE
celload GAZOU+"お城.png",BASIC : buffer CASTLE,BMAG*2,BMAG*2 : gzoom BMAG*2,BMAG*2,BASIC,0,0,64,64

#enum CHARAID
#enum CHARAITEM
celload GAZOU+"キャラクター.png",BASIC : buffer CHARAID,BMAG*5,BMAG*3 : gzoom BMAG*5,BMAG*3,BASIC,0,0,16*5,16*3
celdiv CHARAID,BMAG,BMAG
buffer CHARAITEM,32,32 : gzoom 64,64,BASIC

#enum TIMER
celload GAZOU+"時計.png",BASIC : buffer TIMER,32,32 : gzoom 64,64,BASIC
#enum REDTIMER
celload GAZOU+"赤時計.png",BASIC : buffer REDTIMER,32,32 : gzoom 64,64,BASIC

#enum BACK
celload STAGE+"背景.png",BACK
#enum UPBACK
celload STAGE+"背景上.png",UPBACK
#enum TITLEIMAGE
celload "画像フォルダ\\タイトル.png",TITLEIMAGE

#enum BACKWIN
#enum BACKWINYOBI

#enum LAST

fps_config 60

titlemsecure = BUTTONSIZEY + TITLEM_SECURE

sdim sname
sdim name,200

screen 0,WINDOWX,WINDOWY,,0,0


	;onexit goto *exittt

;***************************************************************************************************
*titlestart2
	dmmplay BGM99
*titlestart
	ov_setvolume 1,-2000
	title GAMENAME

snumber = FIRSTSTAGE
extension = ".txt"
select = "st"+snumber
life = 5
havecoin = 0
interpflag = 0

#if debug == 0

	gsel 0
	clrobj
	objmode 2
	redraw 0
	color $FF,$FF,$FF : boxf
	;タイトル
	gmode 2
	;color 153,217,234 : boxf
	
	color 0,0,0 : boxf
	repeat 2 :i=cnt
		pos 800*i,-420 : celput UPBACK
		pos 800*i,780-600 : celput BACK
	loop

	x = 832
	y = 332
	pos 640-x/2,50
	gzoom x,y,TITLEIMAGE,,,200,80
	redraw 1
	;タイトルメニュー
	sa = TITLEM_NAME
	split sa,",",sname
	font "851ゴチカクット",TITLEM_SIZE-20,0
	objsize BUTTONSIZEX , BUTTONSIZEY , titlemsecure-5
	objimage BUTTON5SCR ,, 0 ,, 100 ,, 200
	color 181,72,0
	pos WINDOWX/2 - BUTTONSIZEX/2 , TITLEM_POS_Y - 15
		button sname.0, *statart
		button sname.1, *stageselect
		button gosub sname.2, *moveinfo
		button gosub sname.3, *info
		button sname.4, *owaru
stop
;****************************
*stageselect
	mmplay OKWAV
	clrobj
	objsize BUTTONSIZEX*3/5 , BUTTONSIZEY
	objimage BUTTON3SCR ,, 0 ,, 100 ,, 200
	pos WINDOWX-BUTTONSIZEX*3/5-STSEL_SECURE , WINDOWY-BUTTONSIZEY-STSEL_SECURE : button "戻る", *titlestartsnd : a = stat
	objsize BUTTONSIZEY , BUTTONSIZEY , BUTTONSIZEY*2
	objimage BUTTON1SCR ,, 0 ,, 100 ,, 200
*stageselect1
	clrobj a+1
	b=0
	pos STSEL_POS_X,STSEL_POS_Y					: button "1", *stage1 : button "8" , *stage8  : button "15", *stage15
	pos STSEL_POS_X+BUTTONSIZEY*2,STSEL_POS_Y	: button "2", *stage2 : button "9" , *stage9  : button "16", *stage16
	pos STSEL_POS_X+BUTTONSIZEY*4,STSEL_POS_Y	: button "3", *stage3 : button "10", *stage10 : button "17", *stage17
	pos STSEL_POS_X+BUTTONSIZEY*6,STSEL_POS_Y	: button "4", *stage4 : button "11", *stage11 : button "18", *stage18
	pos STSEL_POS_X+BUTTONSIZEY*8,STSEL_POS_Y	: button "5", *stage5 : button "12", *stage12 : button "19", *stage19
	pos STSEL_POS_X+BUTTONSIZEY*10,STSEL_POS_Y	: button "6", *stage6 : button "13", *stage13 : button "20", *stage20
	pos STSEL_POS_X+BUTTONSIZEY*12,STSEL_POS_Y	: button "7", *stage7 : button "14", *stage14 : button "次", *stageselect2
stop
;****************************
*stageselect2
	mmplay CCWAV
	clrobj a+1
	b=1
	pos STSEL_POS_X,STSEL_POS_Y					: button "21", *stage1 : button "28" , *stage8 : button "35", *stage15
	pos STSEL_POS_X+BUTTONSIZEY*2,STSEL_POS_Y	: button "22", *stage2 : button "29" , *stage9 : button "36", *stage16
	pos STSEL_POS_X+BUTTONSIZEY*4,STSEL_POS_Y	: button "23", *stage3 : button "30", *stage10 : button "37", *stage17
	pos STSEL_POS_X+BUTTONSIZEY*6,STSEL_POS_Y	: button "24", *stage4 : button "31", *stage11 : button "38", *stage18
	pos STSEL_POS_X+BUTTONSIZEY*8,STSEL_POS_Y	: button "25", *stage5 : button "32", *stage12 : button "39", *stage19
	pos STSEL_POS_X+BUTTONSIZEY*10,STSEL_POS_Y	: button "26", *stage6 : button "33", *stage13 : button "40", *stage20
	pos STSEL_POS_X+BUTTONSIZEY*12,STSEL_POS_Y	: button "27", *stage7 : button "34", *stage14 : button "次", *stageselect3
stop
;****************************
*stageselect3
	mmplay CCWAV
	clrobj a+1
	b=2
	pos STSEL_POS_X,STSEL_POS_Y					: button "41", *stage1 : button "48" , *stage8 : button "55", *stage15
	pos STSEL_POS_X+BUTTONSIZEY*2,STSEL_POS_Y	: button "42", *stage2 : button "49" , *stage9 : button "56", *stage16
	pos STSEL_POS_X+BUTTONSIZEY*4,STSEL_POS_Y	: button "43", *stage3 : button "50", *stage10 : button "57", *stage17
	pos STSEL_POS_X+BUTTONSIZEY*6,STSEL_POS_Y	: button "44", *stage4 : button "51", *stage11 : button "58", *stage18
	pos STSEL_POS_X+BUTTONSIZEY*8,STSEL_POS_Y	: button "45", *stage5 : button "52", *stage12 : button "59", *stage19
	pos STSEL_POS_X+BUTTONSIZEY*10,STSEL_POS_Y	: button "46", *stage6 : button "53", *stage13 : button "60", *stage20
	pos STSEL_POS_X+BUTTONSIZEY*12,STSEL_POS_Y	: button "47", *stage7 : button "54", *stage14 : button "次", *stageselect4
stop
;****************************
*stageselect4
	mmplay CCWAV
	clrobj a+1
	b=3
	pos STSEL_POS_X,STSEL_POS_Y					: button "61", *stage1 : button "68" , *stage8 : button "75", *stage15
	pos STSEL_POS_X+BUTTONSIZEY*2,STSEL_POS_Y	: button "62", *stage2 : button "69" , *stage9 : button "76", *stage16
	pos STSEL_POS_X+BUTTONSIZEY*4,STSEL_POS_Y	: button "63", *stage3 : button "70", *stage10 : button "77", *stage17
	pos STSEL_POS_X+BUTTONSIZEY*6,STSEL_POS_Y	: button "64", *stage4 : button "71", *stage11 : button "78", *stage18
	pos STSEL_POS_X+BUTTONSIZEY*8,STSEL_POS_Y	: button "65", *stage5 : button "72", *stage12 : button "79", *stage19
	pos STSEL_POS_X+BUTTONSIZEY*10,STSEL_POS_Y	: button "66", *stage6 : button "73", *stage13 : button "80", *stage20
	pos STSEL_POS_X+BUTTONSIZEY*12,STSEL_POS_Y	: button "67", *stage7 : button "74", *stage14 : button "次", *stageselect5
stop
;****************************
*stageselect5
	mmplay CCWAV
	clrobj a+1
	b=4
	pos STSEL_POS_X,STSEL_POS_Y					: button "81", *stage1 : button "88" , *stage8 : button "95", *stage15
	pos STSEL_POS_X+BUTTONSIZEY*2,STSEL_POS_Y	: button "82", *stage2 : button "89" , *stage9 : button "96", *stage16
	pos STSEL_POS_X+BUTTONSIZEY*4,STSEL_POS_Y	: button "83", *stage3 : button "90", *stage10 : button "97", *stage17
	pos STSEL_POS_X+BUTTONSIZEY*6,STSEL_POS_Y	: button "84", *stage4 : button "91", *stage11 : button "98", *stage18
	pos STSEL_POS_X+BUTTONSIZEY*8,STSEL_POS_Y	: button "85", *stage5 : button "92", *stage12 : button "99", *stage19
	pos STSEL_POS_X+BUTTONSIZEY*10,STSEL_POS_Y	: button "86", *stage6 : button "93", *stage13 : button "100", *stage20
	pos STSEL_POS_X+BUTTONSIZEY*12,STSEL_POS_Y	: button "87", *stage7 : button "94", *stage14 : button "戻", *stageselect6
stop
*stageselect6
	mmplay CCWAV
	goto *stageselect1
;****************************
*stage20
	snumber+=1
*stage19
	snumber+=1
*stage18
	snumber+=1
*stage17
	snumber+=1
*stage16
	snumber+=1
*stage15
	snumber+=1
*stage14
	snumber+=1
*stage13
	snumber+=1
*stage12
	snumber+=1
*stage11
	snumber+=1
*stage10
	snumber+=1
*stage9
	snumber+=1
*stage8
	snumber+=1
*stage7
	snumber+=1
*stage6
	snumber+=1
*stage5
	snumber+=1
*stage4
	snumber+=1
*stage3
	snumber+=1
*stage2
	snumber+=1
*stage1
	snumber += 20*b
	select = "st"+snumber

	exist STAGE+select+extension
	if strsize = -1 {
		dialog select+extension+"が存在しません\nアプデを期待してね",3,"ざまあwww"
		c = LAST
		if stat==7{
			repeat
				dialog "え？ちゃんとアプデまで待ってくれると思っていたのに…許せない…\nお母さん心配しちゃうよ？いいの？",3,"田舎の母親からの重要なお知らせ"
				if stat==7 : break
				screen c
				gsel c,2
				randomize
				ax=rnd(256)
				bx=rnd(256)
				cx=rnd(256)
				color ax,bx,cx
				boxf
				color 256-ax,256-bx,256-cx
				mes "ちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\nちゃんとアプデを待て    ちゃんとアプデを待て    ちゃんとアプデを待て\n"
				title "親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ親孝行しろ"
				c++
				gsel 0
			loop
			repeat
				if c<=LAST : break
				c--
				gsel c,-1	
			loop
		}
		snumber = FIRSTSTAGE
		if b==0 : goto *stageselect1
		if b==1 : goto *stageselect2
		if b==2 : goto *stageselect3
		if b==3 : goto *stageselect4
		if b==4 : goto *stageselect5
	}

#endif
;***************************************************************************************************
#if debug
	life += 94
	
	bgscr 1,100,160,,100,100
		objsize 30,30
		dim st,7
		st(0)=1
		chkbox "1",st(0)
		chkbox "2",st(1)
		chkbox "4",st(2)
		chkbox "8",st(3)
		pos 30,0
		chkbox "16",st(4)
		chkbox "32",st(5)
		chkbox "64",st(6)
	
		pos 40,100
		input select,50,20
		ia = stat
		objenable ia,0
	
		pos 55,135 : objsize 40,20 : button "適用",*start : objsel stat
	
		repeat
			ib = 0
			foreach st
				ib += st(cnt)*powf(2,cnt)
			loop
			select = "st" + ib
			objprm ia,select
			fps_wait
		loop

#endif

;***************************************************************************************************
*statart
	dmmstop -1
	mmplay OKWAV

*start

#if debug
	gsel 1,-1
#endif

;初期化
cx = 0 : cy = 0
cRUx = 0 : cRUy = 0
cLLx = 0 : cLLy = 0
cRLx = 0 : cRLy = 0
cspeed = 0.0
cspeedy = 0 : jumpflag=0
cdraw = 0
frame = 0
cturn = 0
key = 0
upkey = 0
spacekey = 0
gaovflag = 0
time = 500
interpx = 0
interpy = 0
interpdir = 0 ;なし:0 縦:1 横:2
poleflag = 0

mapdrawx = 0
mapdrawy = 0

gosub *mapload
if interpflag : cx = interpx : cy = interpy

font msgothic,32
random1=rnd(5)
switch random1
	case 0
		dmmloop BGM1,0
		dmmplay BGM1
		swbreak
	case 1
		dmmloop BGM2,0
		dmmplay BGM2
		swbreak
	case 2
		dmmloop BGM3,0
		dmmplay BGM3
		swbreak
	case 3
		dmmloop BGM4,0
		dmmplay BGM4
		swbreak
	default
		dmmloop BGM5,0
		dmmplay BGM5
		swbreak
	
swend

repeat
	fps_wait
	if stat>0: break
loop
;***************************************************************************************************
*main

	redraw 0
	gosub *mapdraw
	gosub *chara
	if(cy>0):gosub *haikei

	;中間処理
	if(interpdir!=0)&(interpflag==0){
		if(interpdir==1)&(cRUx>=interpx){
			mapmain(interpy/BMAG,interpx/BMAG) = INTERP
			mmplay TYWAV
			interpflag=1
		}else:if(interpdir==2)&(cy<=interpy){
			mapmain(interpy/BMAG,interpx/BMAG) = INTERP
			mmplay TYWAV
			interpflag=1
		}
	}

	;アイテム、時間処理
	if time<0	: goto *gameover
	color $FF,$FF,$FF
	pos ginfo(8),ginfo(9)
	if time>100	: celput TIMER	: else	: celput REDTIMER
	mes strf("%03d  ",time),1
	celput CHARAITEM	: mes "x"+life
	pos ginfo(8)+WINDOWX-96,ginfo(9)
	celput COINITEM		: mes "x"+havecoin

	redraw 1

	frame += 1
	if frame >= 60 : frame = 0 : time-=1
	fps_wait

#if debug
	title strf("%3d  ,  %f",stat,fps_get())
#endif
#if debug=0
	title strf("%s    FPS:%.2f",GAMENAME,fps_get())
#endif
	
goto *main

;***************************************************************************************************

*chara
	stick key,5+64
	getkey f1get,112
	getkey f2get,113

	if f1get=1{
		;F1が押されたよ
		gosub *info
	}
	if f2get=1{
		;F2が押されたよ
		dmmstop -1
		mmplay OKWAV
		title GAMENAME
		screen 0,WINDOWX,WINDOWY,,0,0
		goto *titlestart2
	}

*charamove

#if debug
	if key&256 : cx = ginfo(8)+mousex : cy = ginfo(9)+mousey
#endif

	if key&128{
		goto *gameover
	}

	;加速度設定
	cacc = 0.1
	cmaxspeed = 5
	if key&64 : cacc += 0.05 : cmaxspeed += 4
	
	;左
	if key&1{
		cspeed -= cacc
		if(cspeed<-cmaxspeed) : cspeed += cacc+0.01
		if(cspeed<-cmaxspeed) : cspeed += 0.1
	}else:if(cspeed < 0):	cspeed += 0.2 : cspeed = limitf(cspeed,,0)
	;右
	if key&4{
		cspeed += cacc
		if(cspeed>cmaxspeed) : cspeed -= cacc+0.01
		if(cspeed>cmaxspeed) : cspeed -= 0.1
	}else:if(cspeed > 0):	cspeed -= 0.2 : cspeed = limitf(cspeed,0)

	;横移動
	if cspeed<0 : cx += cspeed-1 : else:if cspeed>0 : cx += cspeed+1

	;はみ出し回避
	cx = limit(cx,0,mapyoko-CHARASIZE)

	;x場所更新
	cRUx = cx+CHARASIZE-1
	cLLx = cx
	cRLx = cx+CHARASIZE-1

	;ジャンプ
	if(cy>0){
		if(jumpflag==0){;ジャンプフラグなし
			if((maphit((cLLy+1)/BMAG,(cLLx+1)/BMAG)!=AIR)||(maphit((cRLy+1)/BMAG,(cRLx-1)/BMAG)!=AIR)){;地面ついてる！
				if(key&2)||(key&16){
*nowj
					mmplay JPWAV
					jumpflag=1
					cspeedy-=20+abs(cspeed)/3
				}
			}else{;おめーの地面ねーから
				jumpflag=1
				cspeedy+=1
			}
		}else{;ジャンプフラグあり
			if( ( (maphit((cLLy+1)/BMAG,(cLLx+1)/BMAG)!=AIR) || (maphit((cRLy+1)/BMAG,(cRLx-1)/BMAG)!=AIR) ) && (cspeedy>=0) ){;地面ありき
				jumpflag=0
				cspeedy=0.0
			}else{;down down Mario
				getkey upkey,38
				getkey spacekey,32
				if((upkey|spacekey)==0)&(cspeedy<0) : cspeedy+=2
				cspeedy+=1
			}
		}
	}else:	cspeedy+=1

	;縦移動
	cspeedy = limit(cspeedy,-BMAG+1,15)
	cy += cspeedy

	;y場所更新
	cRUy = cy
	cLLy = cy+BMAG-1
	cRLy = cy+BMAG-1

	;落下判定
	if(cy>maptate) :goto *gameover

	;上配列エラー回避
	if(cy>0){
		;zip回避
		if((maphit(cy/BMAG,cx/BMAG)!=AIR) & (maphit(cRUy/BMAG,cRUx/BMAG)!=AIR) & (maphit(cLLy/BMAG,cLLx/BMAG)!=AIR) & (maphit(cRLy/BMAG,cRLx/BMAG)!=AIR)) : goto *gameover
		;上判定 独自言語解説:[{(どっちか上+飛)+(どっちも下空気)}or{どっちも上+飛}]
		if((((maphit(cy/BMAG,cx/BMAG)!=AIR)|(maphit(cRUy/BMAG,cRUx/BMAG)!=AIR)) & (cspeedy<0) & ((maphit(cLLy/BMAG,cLLx/BMAG)==AIR)&(maphit(cRLy/BMAG,cRLx/BMAG)==AIR)))  |  (((maphit(cy/BMAG,cx/BMAG)!=AIR)&(maphit(cRUy/BMAG,cRUx/BMAG)!=AIR)) & (cspeedy<0))) or ((((mapmain(cy/BMAG,cx/BMAG)==TOMEI)|(mapmain(cRUy/BMAG,cRUx/BMAG)==TOMEI)) & (cspeedy<0) & ((maphit(cLLy/BMAG,cLLx/BMAG)==AIR)&(maphit(cRLy/BMAG,cRLx/BMAG)==AIR)))  |  (( ((mapmain(cy/BMAG,cx/BMAG)==TOMEI)|(maphit(cy/BMAG,cx/BMAG)!=AIR)) & ((maphit(cRUy/BMAG,cRUx/BMAG)==TOMEI)|(maphit(cRUy/BMAG,cRUx/BMAG)!=AIR)) ) & (cspeedy<0))){
			gosub *tataku
			cy = (cy/BMAG+1) * BMAG
			cspeedy = 0
		;左右どっちか壁で入る。ただしどっちもは入らない 独自言語解説:[{(左どっちか)xor(右どっちか)}+{(ジャンプ中)or(左下落下めり込み上ブロック)or(右下落下めり込み上ブロック)}]
		}else:if((((maphit(cy/BMAG,cx/BMAG)!=AIR)|(maphit(cLLy/BMAG,cLLx/BMAG)!=AIR)) ^ ((maphit(cRUy/BMAG,cRUx/BMAG)!=AIR)|(maphit(cRLy/BMAG,cRLx/BMAG)!=AIR)))&((cspeedy<=0)|(maphit((cLLy-15)/BMAG,cLLx/BMAG)!=AIR)|(maphit((cRLy-15)/BMAG,cRLx/BMAG)!=AIR))){

			if((maphit(cy/BMAG,cx/BMAG)!=AIR) | (maphit(cLLy/BMAG,cLLx/BMAG)!=AIR)){
				cx = (cx/BMAG+1) * BMAG
				cspeed = limitf(cspeed,0)
			}else:if((maphit(cRUy/BMAG,cRUx/BMAG)!=AIR) | (maphit(cRLy/BMAG,cRLx/BMAG)!=AIR)){
				cx = (cx/BMAG+1) * BMAG - CHARASIZE
				cspeed = limitf(cspeed,,0)
			}
		;地面めり込み回避
		}else:if(maphit(cLLy/BMAG,cLLx/BMAG)!=AIR)|(maphit(cRLy/BMAG,cRLx/BMAG)!=AIR){
			cy = (cLLy/BMAG-1) * BMAG
		}
	}

	;位置更新
	cRUx = cx+CHARASIZE-1	: cRUy = cy
	cLLx = cx				: cLLy = cy+BMAG-1
	cRLx = cx+CHARASIZE-1	: cRLy = cy+BMAG-1

#if debug
	pos cRUx,cRUy
	if(cy>0){
		if(maphit(cy/BMAG,cx/BMAG)!=AIR):	mes "左上"
		if(maphit(cRUy/BMAG,cRUx/BMAG)!=AIR):	mes "右上"
		if(maphit(cLLy/BMAG,cLLx/BMAG)!=AIR):	mes "左下"
		if(maphit(cRLy/BMAG,cRLx/BMAG)!=AIR):	mes "右下"
		if(maphit((cLLy+1)/BMAG,cLLx/BMAG)!=AIR):	mes "左下地面"
		if(maphit((cRLy+1)/BMAG,cRLx/BMAG)!=AIR):	mes "右下地面"
	}

	color 0,0,$FF
	pset cx,cy : pset cx+1,cy : pset cx,cy+1
	pset cRUx,cRUy : pset cRUx-1,cRUy : pset cRUx,cRUy+1
	pset cLLx,cLLy : pset cLLx+1,cLLy : pset cLLx,cLLy-1
	pset cRLx,cRLy : pset cRLx-1,cRLy : pset cRLx,cRLy-1
#endif

*charadraw

/* 0:右立ち 1,2:右歩き 3:右に切り返し 4:右跳び 5〜9:左同じく 
   10:死 11:右つかまり 12:左つかまり*/

	if(gaovflag | poleflag){
	}else:if(jumpflag){
		if((cdraw>=0)&(cdraw<=3)) : cdraw=4 : else:if((cdraw>=5)&(cdraw<=8)) : cdraw=9
	}else:if cspeed == 0 {
		if((cdraw>=1)&(cdraw<=4)) : cdraw=0 : else:if((cdraw>=6)&(cdraw<=9)) : cdraw=5
	}else:if key&1 {
		if cspeed>0 {
			if key&4 {
				if frame\5 = 0{
				cturn ^= 1
				}
				cdraw = 1+cturn
			}else{
				cdraw = 8
			}
		}else{
			if frame\5 = 0{
				cturn ^= 1
			}
			cdraw = 6+cturn
		}
	}else:if key&4 {
		if cspeed<0 {
			cdraw = 3
		}else{
			if frame\5 = 0{
				cturn ^= 1
			}
			cdraw = 1+cturn
		}
	}else:if((cspeed<0)&(cdraw!=3)){
		if frame\5 = 0{
			cturn ^= 1
		}
		cdraw = 6+cturn
	}else:if((cspeed>0)&(cdraw!=8)){
		if frame\5 = 0{
			cturn ^= 1
		}
		cdraw = 1+cturn
	}

	gmode 2,BMAG,BMAG
	pos cx-CHARASHIFT,cy : celput CHARAID,cdraw

	if((cx < ginfo(8)+double(WINDOWX)*0.4) & (ginfo(8)>0))							{ groll cx-double(WINDOWX)*0.4,ginfo(9)
	}else : if((cx > ginfo(8)+double(WINDOWX)*0.6) & (ginfo(8)<mapyoko-WINDOWX))	: groll cx-double(WINDOWX)*0.6,ginfo(9)
	if((cy < ginfo(9)+double(WINDOWY)*0.4) & (ginfo(9)>0))							{ groll ginfo(8),cy-double(WINDOWY)*0.4
	}else : if((cy > ginfo(9)+double(WINDOWY)*0.6) & (ginfo(9)<maptate-WINDOWY))	: groll ginfo(8),cy-double(WINDOWY)*0.6

return

;***************************************************************************************************
*haikei
	;コイン判定
	if(mapmain(cy/BMAG,cx/BMAG)==COIN):mapmain(cy/BMAG,cx/BMAG)==AIR:havecoin++:mmplay COWAV
	if(mapmain(cRUy/BMAG,cRUx/BMAG)==COIN):mapmain(cRUy/BMAG,cRUx/BMAG)==AIR:havecoin++:mmplay COWAV
	if(mapmain(cLLy/BMAG,cLLx/BMAG)==COIN):mapmain(cLLy/BMAG,cLLx/BMAG)==AIR:havecoin++:mmplay COWAV
	if(mapmain(cRLy/BMAG,cRLx/BMAG)==COIN):mapmain(cRLy/BMAG,cRLx/BMAG)==AIR:havecoin++:mmplay COWAV

	if(havecoin>=100) : havecoin -= 100 : life+=1

	;マグマ判定
	if(mapmain(cy/BMAG,cx/BMAG)==MAGMA):goto *gameover
	if(mapmain(cRUy/BMAG,cRUx/BMAG)==MAGMA):goto *gameover
	if(mapmain(cLLy/BMAG,cLLx/BMAG)==MAGMA):goto *gameover
	if(mapmain(cRLy/BMAG,cRLx/BMAG)==MAGMA):goto *gameover
	if(mapmain(cy/BMAG,cx/BMAG)==UNDERMAGMA):goto *gameover
	if(mapmain(cRUy/BMAG,cRUx/BMAG)==UNDERMAGMA):goto *gameover
	if(mapmain(cLLy/BMAG,cLLx/BMAG)==UNDERMAGMA):goto *gameover
	if(mapmain(cRLy/BMAG,cRLx/BMAG)==UNDERMAGMA):goto *gameover
	if(mapmain(cy/BMAG,cx/BMAG)==MAGMAB):goto *gameover
	if(mapmain(cRUy/BMAG,cRUx/BMAG)==MAGMAB):goto *gameover
	if(mapmain(cLLy/BMAG,cLLx/BMAG)==MAGMAB):goto *gameover
	if(mapmain(cRLy/BMAG,cRLx/BMAG)==MAGMAB):goto *gameover
	
	;ジャンプブロック判定
	if(mapmain((cLLy+1)/BMAG,cLLx/BMAG)==JUMPB):cspeedy=-65535:goto *nowj
	if(mapmain((cRLy+1)/BMAG,cRLx/BMAG)==JUMPB):cspeedy=-65535:goto *nowj

	;ゴール判定
	if(mapmain(cRUy/BMAG,(cRUx-BMAG/2)/BMAG)==GOALPAUL):goto *goalok
	if(mapmain(cRLy/BMAG,(cRLx-BMAG/2)/BMAG)==GOALPAUL):goto *goalok

return
;***************************************************************************************************
*tataku
	;ジャンプ中の当たり判定
	if(mapmain(cy/BMAG,cx/BMAG)==HATENA)	: mapmain(cy/BMAG,cx/BMAG)=NOITEM:havecoin++:mmplay COWAV
	if(mapmain(cRUy/BMAG,cRUx/BMAG)==HATENA): mapmain(cRUy/BMAG,cRUx/BMAG)=NOITEM:havecoin++:mmplay COWAV

	if(mapmain(cy/BMAG,cx/BMAG)==TOMEI)		: maphit(cy/BMAG,cx/BMAG)=BASIC		: mapmain(cy/BMAG,cx/BMAG)=NOITEM		: havecoin++:mmplay COWAV
	if(mapmain(cRUy/BMAG,cRUx/BMAG)==TOMEI)	: maphit(cRUy/BMAG,cRUx/BMAG)=BASIC	: mapmain(cRUy/BMAG,cRUx/BMAG)=NOITEM	: havecoin++:mmplay COWAV

return
;***************************************************************************************************
*goalok
	;redraw 0
	dmmstop -1
	mmplay CLWAV
	key = 0
	cspeedy = 7
	poleflag = 1
	if(cspeed>=0) : cdraw=11 : else : cdraw=12
	cspeed = 0.0
	repeat 70
		redraw 0
		gosub *mapdraw
		gosub *charamove
		cspeedy = 7
		redraw 1
		
		frame += 1
		if frame >= 60 : frame = 0
		fps_wait
	loop

	cdraw = 0
	poleflag = 0
	key = 4
	repeat 300
		redraw 0
		gosub *mapdraw
		if((mapmain(cRUy/BMAG,cRUx/BMAG)!=CASTLE)&&(mapmain(cRLy/BMAG,cRLx/BMAG)!=CASTLE))	: gosub *charamove
		redraw 1
		frame += 1
		if frame >= 60 : frame = 0
		fps_wait
	loop

	snumber+=1
	if snumber>100 : goto *gameset
	select = "st"+snumber
	exist "ステージ\\"+select+".txt"
	;dialog "ステージ\\"+select+".txt\n"+strsize
	if strsize = -1:goto *gameset
	interpflag=0

	goto *start
		
;***************************************************************************************************
*gameset
	await 2000
	screen 0,WINDOWX,WINDOWY,,0,0
	color 0,0,0
	boxf
	color 255,255,255
	fsize=48
	font msgothic,fsize,0
	;dialog ""+(WINDOWX/2-BUTTONSIZEX/2)+"\n"+TITLEM_POS_Y
	omedeto="おめでとう！ゲームクリアだ！"
	pos WINDOWX/2-(strlen(omedeto)*5*(fsize/20)) , WINDOWY/2:mes omedeto
	font msgothic,fsize/2,0
	shokin="賞金は0円だぜ！"
	pos WINDOWX/2-(strlen(shokin)*5.0*(fsize/20/2)) , WINDOWY/2+fsize:mes shokin
	mmplay ACWAV
	wait 700
	color 0,0,0
	boxf
	color 255,255,255
	fsize=48
	font msgothic,fsize,0
	;dialog ""+(WINDOWX/2-BUTTONSIZEX/2)+"\n"+TITLEM_POS_Y
	omedeto="プレイしてくれてありがとな"
	pos WINDOWX/2-(strlen(omedeto)*5*(fsize/20)) , WINDOWY/2:mes omedeto
	font msgothic,fsize/2,0
	shokin="良かったら友達に拡散してね"
	pos WINDOWX/2-(strlen(shokin)*5.0*(fsize/20/2)) , WINDOWY/2+fsize:mes shokin
	mmplay EDWAV
	wait 900
	goto *titlestart2

;***************************************************************************************************
*gameover
	dmmstop -1
	mmplay DTWAV
	gaovflag=1 : cdraw=10
	life-=1
	repeat 20
		redraw 0
		gosub *mapdraw
		if(cy<maptate):gosub *charadraw
		redraw 1
		fps_wait
	loop
	repeat 100,-20
		redraw 0
		gosub *mapdraw
		if(cy<maptate){
			cy+=limit(cnt,,10)
			gosub *charadraw
		}
		redraw 1
		fps_wait
	loop
	if life<0 : goto *gameend
	goto *start
	
;***************************************************************************************************

*mapload

	screen 0,WINDOWX,WINDOWY,0,0,0 : cls 4
	color $FF,$FF,$FF
	sa = "ステージ"+snumber
	ssize = 64
	font msgothic,ssize
	pos ginfo(12)/2-(strlen(sa)*(ssize/2))/2,ginfo(13)/2-ssize
	mes sa
	sa = "残り  x"+life
	font msgothic,BMAG
	pos ginfo(12)/2-(strlen(sa)*(BMAG/2))/2,ginfo(13)/2
	mes "残り",1 : celput CHARAID,0 : mes "x"+life
	
	await

	dim mapmain,2048,512
	dim maphit,2048,512
	sdim mapindex,2048
	sdim mapin,1048576 ;2048*512

	notesel mapin
	noteload STAGE+select+extension,1048576
	noteunsel

	ca = "\n"
	split mapin,ca,mapindex
	maphigh = stat
	maplen = strlen(mapindex)

	mapyoko = maplen*BMAG
	maptate = maphigh*BMAG

	buffer BASIC,mapyoko,maptate
	randomize
	mhaikei = rnd(3)
	if mhaikei==0 : color 153,217,234 : else:if mhaikei==1 : color 255,127,39 : else : color 1,1,1
	boxf

	backx = 800
	backy = 600

	gmode 2
	repeat :i=cnt
		if(mapyoko<backx*cnt):break

		repeat ,2:j=cnt
			pos backx*i,maptate-backy*j : celput UPBACK
			if(maptate<backy*j):break
		loop
		
		pos backx*i,maptate-backy : celput BACK
	loop
	buffer BACKWIN,maplen*16,maphigh*16 : cls 4
	repeat maphigh : i=cnt
		iminus=i-1
		repeat maplen : j=cnt
			ca = strmid(mapindex.i,j,1)
			if(i==0){
				caminus = "z"
			}else{
				caminus = strmid(mapindex.iminus,j,1)
			}
			pos j*16,i*16
			mapmain(i,j)=BASIC
			maphit(i,j)=BASIC
			if			ca=="W"{	if(caminus=="W"){
										picload GAZOU + "土ブロック.png",1
									}else{
										picload GAZOU + "草ブロック.png",1
									}
			}else : if	ca=="O"{	picload GAZOU + "固ブロック.png",1
			}else:if	ca=="{"{	picload GAZOU + "土管左上.png",1
			}else:if	ca=="}"{	picload GAZOU + "土管右上.png",1
			}else:if	ca=="["{	picload GAZOU + "土管左下.png",1
			}else:if	ca=="]"{	picload GAZOU + "土管右下.png",1
			/* 変化なし&1マス判定 ブロック追加の説明
			}else:if    ca=="メモの文字"{	picload GAZOU + ファイルの名前",1 (ファイルは事前に画像フォルダに入れておく) */
			/* 変化ありブロックの追加の説明
			}else:if	ca=="メモの文字"{	mapmain(i,j) = enum名前 */
			}else:if	ca=="b"{	mapmain(i,j) = BRICK
			}else:if	ca=="?"{	mapmain(i,j) = HATENA
			}else:if	ca=="_"{	mapmain(i,j) = TOMEI : maphit(i,j) = AIR
			}else:if	ca=="M"{	mapmain(i,j) = MAGMA : maphit(i,j) = AIR
			}else:if	ca=="m"{	mapmain(i,j) = MAGMAB : maphit(i,j) = AIR
			}else:if	ca=="J"{	mapmain(i,j) = UNDERMAGMA : maphit(i,j) = AIR
			}else:if	ca=="$"{	mapmain(i,j) = COIN : maphit(i,j) = AIR
			}else:if	ca=="^"{	mapmain(i,j) = JUMPB
			}else:if	ca=="|"{	mapmain(i,j) = GOALPAUL : maphit(i,j) = GOAL
			}else:if	ca=="o"{	mapmain(i,j-1) = GOALBALL : maphit(i,j) = GOAL
			}else:if	ca=="!"{	pos ginfo(22)-16*3,ginfo(23)-16*5 : celput CASTLE : mapmain(i,j) = CASTLE : maphit(i,j) = AIR
			}else:if	ca=="P"{	cx = j*BMAG : cy = i*BMAG : mapmain(i,j) = AIR : maphit(i,j) = AIR
			}else:if	ca=="R"{	interpx = j*BMAG : interpy = i*BMAG : mapmain(i,j) = INTERPMAE : maphit(i,j) = AIR : interpdir = 1
			}else:if	ca=="9"{	interpx = j*BMAG : interpy = i*BMAG : mapmain(i,j) = INTERPMAE : maphit(i,j) = AIR : interpdir = 2
			}else:if	ca==" "{	mapmain(i,j) = AIR : maphit(i,j) = AIR
			}else{	mapmain(i,j)=ERROR
			}
		loop
	loop
	buffer BACKWINYOBI,mapyoko,maptate
	gzoom mapyoko,maptate,BACKWIN,0,0,maplen*16,maphigh*16
	gsel BASIC
	gmode 2,mapyoko,maptate 
	pos 0,0
	celput BACKWINYOBI

	if interpflag : mapmain(interpy/BMAG,interpx/BMAG) = INTERP

	await 1500
	screen 0,mapyoko,maptate,0,0,0,WINDOWX,WINDOWY

return

*mapdraw

	mapdrawx = ginfo(8)-3*BMAG
	if mapdrawx<0 : mapdrawx = 0
	mapdrawy = ginfo(9)-3*BMAG
	if mapdrawy<0 : mapdrawy = 0
	pos mapdrawx,mapdrawy
	gcopy BASIC,mapdrawx,mapdrawy,BMAG*32,BMAG*20

	gmode 2,BMAG,BMAG
	repeat 21 : i=cnt+mapdrawy/BMAG
		repeat 32 : j=cnt+mapdrawx/BMAG
			pos j*BMAG,i*BMAG
			if((mapmain(i,j)!=BASIC)&(mapmain(i,j)!=AIR)&(mapmain(i,j)!=CASTLE)) : celput mapmain(i,j)
		loop
	loop
return

;***************************************************************************************************
*gameend
	cls 4
	groll 0,0
	wait 200
	mmplay GOWAV
	sa = "GAME OVER"
	ssize = 64
	color $FF,$FF,$FF
	font msgothic,ssize
	pos ginfo(12)/2-(strlen(sa)*(ssize/2))/2,ginfo(13)/2-ssize/2
	mes sa
	wait 600
	color 0,0,0:boxf
	sa = "画面真っ黒って自分の顔が反射するからなんか嫌だよね"
	ssize = 48
	color $FF,$FF,$FF
	font msgothic,ssize
	pos ginfo(12)/2-(strlen(sa)*(ssize/2))/2,ginfo(13)/2-ssize/2
	mes sa
	wait 600
	color 0,0,0:boxf
	sa = "まあ、次こそクリアできるよ。"
	ssize = 64
	color $FF,$FF,$FF
	font msgothic,ssize
	pos ginfo(12)/2-(strlen(sa)*(ssize/2))/2,ginfo(13)/2-ssize/2
	mes sa
	wait 600
	color 0,0,0:boxf
	sa = "柿の種っておいしいよね"
	ssize = 64
	color $FF,$FF,$FF
	font msgothic,ssize
	pos ginfo(12)/2-(strlen(sa)*(ssize/2))/2,ginfo(13)/2-ssize/2
	mes sa
	wait 600
	color 0,0,0:boxf
	sa = "プレイしてくれてありがとう"
	sb = "良かったら拡散してね"
	ssize = 64
	color $FF,$FF,$FF
	font msgothic,ssize
	pos ginfo(12)/2-(strlen(sa)*(ssize/2))/2,ginfo(13)/2-ssize/2
	mes sa
	font msgothic,ssize/2
	pos ginfo(12)/2-(strlen(sb)*(ssize/4))/2,ginfo(13)/2+ssize/2
	mes sb
	wait 800
	boxf
	mmstop
	screen 0,WINDOWX,WINDOWY,,0,0
	goto *titlestart2
;***************************************************************************************************
*owaru
	end
/***************************************************************************************************

井長州力 //これをハッシュドタグといいます
やあ、マイケルぅ！やあボブ！
もうプログラム言語のすべてがわかったわ(・ω・`)?
https://caramelos.xyz

*/

*titlestartsnd
	mmplay CCWAV
	goto *titlestart

*moveinfo
	dialog "左右キーで移動\nスペースまたは上でジャンプ\nCtrlでダッシュ\nプレイ中にESCで自殺\nプレイ中にF1で製作者情報\nプレイ中にF2でタイトル画面に戻る",,"操作説明"
	return

*info
	dialog DEVELOPER,,"製作者情報"
	return
	
*exittt
	;dialog "本当に終わるの？",3,"まじかよ"
	;if stat=6:end
	dmmbye
	end










	;2020/12/05 1000行達成！！！おめでとうございます！ここが1000行目です！